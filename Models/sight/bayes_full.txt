		model{
		
			#  Group size model, strat specific
			#  Priors
			for(h in 1:nstrat){
				lambda_a[h] ~ dunif(.001, 400)
				lambda_b[h] ~ dunif(.001, 100)
				lambda[h] ~ dgamma(lambda_a[h], lambda_b[h])
			}
			
			#  Loop over groups, log of group size on left
			for(j in 1:ngroups){
				y[j] ~ dpois(lambda[strat[j]])
			}			
			
			#  Models for imputation of missing covariate values
			for(h in 1:nstrat){
				#  Strat specific prior on probability of group authenticity
				psi[h] ~ dbeta(1, 1)

			}
			
			#  Coefficients, informative priors from prior sightability analysis
			b ~ dmnorm(est_coef[], est_sigma[,])
			
			for(j in 1:ngroups){
				#  Indicator value for groups authenticity
				q[j] ~ dbern(psi[strat[j]])
				
				#  Priors on covariate values
				obs_act[j] ~ dcat(pact[strat[j],])
				obs_veg[j] ~ dcat(pveg[strat[j],])
				obs_snow[j] ~ dcat(psnow[strat[j],])
				
				#  Linear predictor on sightability
				logit(p[j]) <- b[1] +
					b[obs_act[j]] * (obs_act[j] > 1) +
					b[obs_veg[j] + 2] * (obs_veg[j] > 1) +
					b[obs_snow[j] + 6] * (obs_snow[j] > 1) +
					b[10] * y[j]

				#  Observed group state, real and not
				z[j] ~ dbern(q[j] * p[j])
			
			}

			#  Stratum population size derivation
			for(h in 1:nstrat){
				for(j in 1:ngroups){
					n_strat[h,j] <- (y[j] + 1) * q[j] * equals(strat[j], h)
				}
				#  Strat specific pop estimates
				N_strat[h] <- sum(n_strat[h,])				
			}
			
			#  Total sampled population estimate	
			N_sampled <- sum(N_strat[])
			N_total <- N_sampled/0.87
			N_imputed <- N_total - N_sampled
			
			#  Total number of groups
			M <- sum(q[])
			
			#  Proportion of groups observed - sightability
			prop_grps <- sum(z[])/M
			
		}